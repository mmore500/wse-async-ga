// memory layout ===============================================================
// byte 0 FITNESS (f32, 4 bytes)
// byte 1
// byte 2
// byte 3
// byte 4 COUNTER (u32, 4 bytes)
// byte 5
// byte 6
// byte 7
// byte 8 BITFIELD (8 bytes)
// byte 9
// byte 10
// byte 11
// byte 12
// byte 13
// byte 14
// byte 15
// TOTAL: 4 words -> 16 bytes

// TODO why can't this be 4 words?
// TODO see https://github.com/mmore500/wse-async-ga/issues/4
//! {
//!     "dstream_algo": (
//!         "dstream." + compconf_data.get(
//!             "CEREBRASLIB_HSTRAT_DSTREAM_ALGO_NAME", "tilted_algo"
//!         ),
//!         pl.Categorical,
//!     ),
//!     "downstream_validate_exploded": (
//!         {
//!             "block": "pl.col('dstream_value') == pl.col('dstream_Tbar') // 33",
//!             "checkerboard": "pl.col('dstream_value') == pl.col('dstream_Tbar') % 2",
//!         }[
//!             compconf_data.get("CEREBRASLIB_DSTREAM_VALIDATOR", "checkerboard")
//!         ],
//!         pl.Categorical,
//!     ),
//!     "downstream_validate_unpacked": (
//!         f"""pl.col('dstream_T') == {
//!             compconf_data.get("CEREBRASLIB_DSTREAM_EXPECTED_T", 64)
//!         }""",
//!        pl.Categorical,
//!     ),
//!     "dstream_storage_bitoffset": (64, pl.UInt8),
//!     "dstream_storage_bitwidth": (64, pl.UInt8),
//!     "dstream_T_bitoffset": (32, pl.UInt8),
//!     "dstream_T_bitwidth": (32, pl.UInt8),
//!     "dstream_S": (64, pl.UInt8),
//!     "nWav": (5, pl.UInt8),
//! }

// module imports =============================================================
const math = @import_module("<math>");
const random = @import_module("<random>");

const compconf = @import_module("<compconf>");
comptime { // access via get_value for logging...
    const _ = compconf.get_value(
        "CEREBRASLIB_HSTRAT_DSTREAM_ALGO_NAME",
        comptime_string,
    );
} // ... raw string works around sdk v1.0.0 limitation
const compconf_raw = @import_module("<compconf_raw>");
const has_value = compconf.has_value("CEREBRASLIB_HSTRAT_DSTREAM_ALGO_NAME");
const dstream_algo_name = (if (has_value)
    compconf_raw.CEREBRASLIB_HSTRAT_DSTREAM_ALGO_NAME
else
    "tilted_algo");
const dstream_algo = @import_module(
    @strcat("<downstream/dstream/", dstream_algo_name, ">"),
);
const dstream_expected_T = compconf.get_value_or(
    "CEREBRASLIB_DSTREAM_EXPECTED_T",
    @as(u8, 64),
);
const validator = compconf.get_value_or(
    "CEREBRASLIB_DSTREAM_VALIDATOR",
    "checkerboard",
);

const bitmanip = @import_module("cerebraslib/bitmanip.csl");
const opscalar = @import_module("cerebraslib/opscalar.csl");
const tilted = @import_module("cerebraslib/tilted_sticky.csl");

// module config specs ========================================================
const gSize: u16 = 5;
const genome_t = [gSize]u32;
const genomePtr_t = [*]u32;
const nTraitVals: u16 = 1;

// internal functions =========================================================
fn _apply_mutation(genome: genomePtr_t) void {
    var mutated = calc_fitness_of(genome);
    const scale = (mutated + 1.0) / 1024.0;

    if (opscalar.p(0.3)) {
        mutated -= math.abs(random.random_normal_f32()) * scale;
    }
    // mutate fitness value
    @ptrcast(*f32, genome).* = mutated;
}

fn _step_instrumentation(genome: genomePtr_t) void {
    // elapse generation in bitfield
    const S = 64;
    const T = @ptrcast([*]u32, genome)[1];
    const site = dstream_algo.assign_storage_site(S, T);

    const offset_bits: u16 = (4 + 4) * 8;
    const target_bit = @as(u16, site) + offset_bits;

    const value = if (validator == "block")
        T / 33 == 1
    else if (validator == "checkerboard")
        T % 2 == 1
    else
        false;

    const genome_bytes = @ptrcast([*]u8, genome);
    if (site != S) bitmanip.set_nth_bit_bigendian(
        genome_bytes,
        target_bit,
        value,
    );

    // elapse generation in counter
    @ptrcast([*]u32, genome)[1] += 1;
}

// public API =================================================================
fn elapse_inheritance_of(genome: genomePtr_t) void {
    _apply_mutation(genome);
}

fn calc_fitness_of(genome: genomePtr_t) f32 {
    return @ptrcast(*f32, genome).*;
}

fn get_trait_value(genome: genomePtr_t) u32 {
    return 0;
}

fn _swap_bytes(a: *u8, b: *u8) void {
    var tmp: u8 = 0;
    tmp = a.*;
    a.* = b.*;
    b.* = tmp;
}

fn initialize_one(genome: genomePtr_t) void {
    @ptrcast(*f32, genome).* = 0.0;
    @ptrcast([*]u32, genome)[1] = 0;
    @ptrcast([*]u32, genome)[2] = 0;
    @ptrcast([*]u32, genome)[3] = 0;
    for (@range(u8, dstream_expected_T)) |_| {
        _step_instrumentation(genome);
    }
    var genome_bytes = @ptrcast([*]u8, genome);
    // TODO why is this necessary?
    // see https://github.com/mmore500/wse-async-ga/issues/5
    _swap_bytes(&genome_bytes[9], &genome_bytes[13]);
    _swap_bytes(&genome_bytes[10], &genome_bytes[12]);
}
