// memory layout ===============================================================
// byte 0 FITNESS (f32, 4 bytes)
// byte 1
// byte 2
// byte 3
// byte 4 DSTREAM_T COUNTER (u32, 4 bytes)
// byte 5
// byte 6
// byte 7
// byte 8 BITFIELD (8 bytes)
// byte 9
// byte 10
// byte 11
// byte 12
// byte 13
// byte 14
// byte 15
// byte 16 GENERATION COUNTER (u32, 4 bytes)
// byte 17
// byte 18
// byte 19
// TOTAL: 5 words -> 20 bytes

//! {
//!     "dstream_algo": (
//!         "dstream." + compconf_data.get(
//!             "CEREBRASLIB_HSTRAT_DSTREAM_ALGO_NAME:comptime_string", "tilted_algo"
//!         ),
//!         pl.Categorical,
//!     ),
//!     "dstream_storage_bitoffset": (64, pl.UInt8),
//!     "dstream_storage_bitwidth": (64, pl.UInt8),
//!     "dstream_T_bitoffset": (32, pl.UInt8),
//!     "dstream_T_bitwidth": (32, pl.UInt8),
//!     "dstream_S": (64, pl.UInt8),
//!     "hstrat_T_dilation": (
//!         compconf_data.get("CEREBRASLIB_HSTRAT_T_DILATION:u32", 1),
//!         pl.UInt32,
//!     ),
//!     "nWav": (5, pl.UInt8),
//! }

// module imports =============================================================
const math = @import_module("<math>");
const random = @import_module("<random>");

const compconf = @import_module("<compconf>");
comptime { // access via get_value for logging...
    const _ = compconf.get_value_or(
        "CEREBRASLIB_HSTRAT_DSTREAM_ALGO_NAME",
        "tilted_algo",
    );
} // ... raw string works around sdk v1.0.0 limitation
const compconf_raw = @import_module("<compconf_raw>");
const has_value = compconf.has_value("CEREBRASLIB_HSTRAT_DSTREAM_ALGO_NAME");
const dstream_algo_name = (if (has_value)
    compconf_raw.CEREBRASLIB_HSTRAT_DSTREAM_ALGO_NAME
else
    "tilted_algo");
const dstream_algo = @import_module(
    @strcat("<downstream/dstream/", dstream_algo_name, ">"),
);
const hstrat_T_dilation = compconf.get_value_or(
    "CEREBRASLIB_HSTRAT_T_DILATION",
    @as(u32, 1),
);

const bitmanip = @import_module("cerebraslib/bitmanip.csl");
const opscalar = @import_module("cerebraslib/opscalar.csl");

const S = 64;

// module config specs ========================================================
const gSize: u16 = 5;
const genome_t = [gSize]u32;
const genomePtr_t = [*]u32;
const nTraitVals: u16 = 1;
const hasImmClobber: bool = false;

// internal functions =========================================================
fn _apply_mutation(genome: genomePtr_t) void {
    var mutated = calc_fitness_of(genome);
    const scale = (mutated + 1.0) / 1024.0;

    if (opscalar.p(0.003)) {
        mutated += math.abs(random.random_normal_f32()) * scale;
    }
    if (opscalar.p(0.3)) {
        mutated -= math.abs(random.random_normal_f32()) * scale;
    }
    // mutate fitness value
    @ptrcast(*f32, genome).* = mutated;
}

fn _do_step_instrumentation(genome: genomePtr_t) void {
    // elapse generation in bitfield
    if (opscalar.unifbool()) {
        const T = @ptrcast([*]u32, genome)[1];
        const site = dstream_algo.assign_storage_site(S, T);

        const offset_bits: u16 = (4 + 4) * 8;
        const target_bit = @as(u16, site) + offset_bits;

        const genome_bytes = @ptrcast([*]u8, genome);
        if (site != S) bitmanip.flip_nth_bit_with_swaps(genome_bytes, target_bit, .{ .byteSwap = true, .bitSwap = true });
    }

    // elapse generation in counter
    @ptrcast([*]u32, genome)[1] += 1;
}

fn _step_instrumentation(genome: genomePtr_t) void {
    if (@ptrcast([*]u32, genome)[4] % hstrat_T_dilation == 0) {
        _do_step_instrumentation(genome);
    }
    @ptrcast([*]u32, genome)[4] += 1;
}

// public API =================================================================
fn elapse_inheritance_of(genome: genomePtr_t) void {
    _apply_mutation(genome);
    _step_instrumentation(genome);
}

fn calc_fitness_of(genome: genomePtr_t) f32 {
    return @ptrcast(*f32, genome).*;
}

fn get_trait_value(genome: genomePtr_t) u32 {
    return 0;
}

fn initialize_one(genome: genomePtr_t) void {
    const gHalfWords: u16 = gSize * 2;
    bitmanip.randomize(@ptrcast([*]u16, genome), gHalfWords);
    @ptrcast(*f32, genome).* = 0.0;
    @ptrcast([*]u32, genome)[1] = 0;
    @ptrcast([*]u32, genome)[4] = 0;
    for (@range(u16, S)) |_| {
        _do_step_instrumentation(genome);
    }
}

fn clobber_immigrant(resident: genomePtr_t, immigrant: genomePtr_t) void {}
